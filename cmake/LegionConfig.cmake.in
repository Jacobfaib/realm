list(INSERT CMAKE_MODULE_PATH 0 @Legion_SOURCE_DIR@/cmake)

# HWLOC is a private dependency and only need to be pulled in for static
# builds
if(NOT @BUILD_SHARED_LIBS@)
  set(Legion_USE_HWLOC @Legion_USE_HWLOC@)
  if(Legion_USE_HWLOC)
    set(HWLOC_INCLUDE_DIR @HWLOC_INCLUDE_DIR@)
    set(HWLOC_LIBRARY @HWLOC_LIBRARY@)
    find_package(HWLOC REQUIRED)
  endif()
endif()

# GASNet is a public dependency and always needs to be pulled in
set(Legion_USE_GASNet @Legion_USE_GASNet@)
if(Legion_USE_GASNet)
  set(GASNet_CONDUIT @GASNet_CONDUIT@)
  set(GASNet_THREADING @GASNet_THREADING@)
  set(GASNet_INCLUDE_DIR @GASNet_INCLUDE_DIR@)
  find_package(GASNet REQUIRED)
endif()

# LLVM is a private dependency and only need to be pulled in for static
# builds
if(NOT @BUILD_SHARED_LIBS@)
  set(Legion_USE_LLVM @Legion_USE_LLVM@)
  if(Legion_USE_LLVM)
    set(Legion_LLVM_COMPONENTS @Legion_LLVM_COMPONENTS@)
    set(LLVM_CONFIG_EXECUTABLE @LLVM_CONFIG_EXECUTABLE@)
    find_package(LLVM REQUIRED COMPONENTS ${Legion_LLVM_COMPONENTS})
  endif()
endif()

# CUDA is only a private dependency of Legion but has a few usage requirements
# so we add it in here
set(Legion_USE_CUDA @Legion_USE_CUDA@)
if(Legion_USE_CUDA)
  set(CUDA_USE_STATIC_CUDA_RUNTIME @CUDA_USE_STATIC_CUDA_RUNTIME@)
  find_package(CUDA REQUIRED)
endif()

include(@Legion_BINARY_DIR@/LegionTargets.cmake)
if(NOT TARGET Legion)
  add_library(Legion INTERFACE IMPORTED)
  set_target_properties(Legion PROPERTIES
    INTERFACE_LINK_LIBRARIES Legion::HighLevelRuntime
  )
endif()

# CUDA has some issues propogating target interface flags properties so we
# have to extract them from the target and manually add them to the nvcc
# flags
if(Legion_USE_CUDA)
  set(CUDA_NVCC_FLAGS "${CUDA_NVCC_FLAGS} $<$<BOOL:$<TARGET_PROPERTY:HighLevelRuntime,COMPILE_OPTIONS>>:$<TARGET_PROPERTY:HighLevelRuntime,COMPILE_OPTIONS>>")
  set(CUDA_NVCC_FLAGS "${CUDA_NVCC_FLAGS} $<$<BOOL:$<TARGET_PROPERTY:HighLevelRuntime,COMPILE_DEFINITIONS>>:-D$<JOIN:$<TARGET_PROPERTY:HighLevelRuntime,COMPILE_DEFINITIONS>, -D>>")
  set(CUDA_NVCC_FLAGS "${CUDA_NVCC_FLAGS} $<$<BOOL:$<TARGET_PROPERTY:HighLevelRuntime,INCLUDE_DIRECTORIES>>:-I$<JOIN:$<TARGET_PROPERTY:HighLevelRuntime,INCLUDE_DIRECTORIES>, -I>>")
endif()
